#
# Set Vendor Name
# Should be set by setting WHATEVER_VENDOR.
#

if(NOT CPACK_PACKAGE_VENDOR)
	if(WHATEVER_VENDOR)
		set(CPACK_PACKAGE_VENDOR "${WHATEVER_VENDOR}")
	else()
		set(CPACK_PACKAGE_VENDOR "Unknown")
	endif()
endif()

#
# Set Contact Email
# Should be set by setting WHATEVER_CONTACT.
#

if(NOT CPACK_PACKAGE_CONTACT)
	if(WHATEVER_CONTACT)
		set(CPACK_PACKAGE_CONTACT "${WHATEVER_CONTACT}")
	endif()
endif()

#
# Set Description
# Should be set by setting WHATEVER_DESCRIPTION.
#

if(NOT CPACK_PACKAGE_DESCRIPTION_SUMMARY)
	if(WHATEVER_DESCRIPTION)
		set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${WHATEVER_DESCRIPTION}")
	else()
		set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Whatever...")
	endif()
endif()

#
# Set License File
#
if(NOT CPACK_RESOURCE_FILE_LICENSE)
	if(WHATEVER_LICENSE)
		set(CPACK_RESOURCE_FILE_LICENSE "${PROJECT_SOURCE_DIR}/${WHATEVER_LICENSE}")
	else()
		set(CPACK_RESOURCE_FILE_LICENSE "${PROJECT_SOURCE_DIR}/LICENSE.md")
	endif()
endif()

#
# Set Package Types
#

cmake_dependent_option(BUILD_PACKAGE_NSIS "Create NSIS Installer (.EXE)" ON WIN32 OFF)
cmake_dependent_option(BUILD_PACKAGE_WIX "Create WiX Installer (.MSI)" ON WIN32 OFF)
cmake_dependent_option(BUILD_PACKAGE_DEB "Create Debian Package (.DEB)" ON UNIX OFF)
cmake_dependent_option(BUILD_PACKAGE_RPM "Create Red Hat Package (.RPM)" ON UNIX OFF)

if(WIN32)
	set(CPACK_GENERATOR "ZIP")
else()
	set(CPACK_GENERATOR "TGZ")
	set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
endif()

if(BUILD_PACKAGE_NSIS)
	list(APPEND CPACK_GENERATOR "NSIS")
endif()
if(BUILD_PACKAGE_WIX)
	list(APPEND CPACK_GENERATOR "WIX")
endif()
if(BUILD_PACKAGE_DEB)
	list(APPEND CPACK_GENERATOR "DEB")
	set(CPACK_DEB_COMPONENT_INSTALL ON)
endif()
if(BUILD_PACKAGE_RPM)
	list(APPEND CPACK_GENERATOR "RPM")
	set(CPACK_RPM_COMPONENT_INSTALL ON)
endif()

#
# Basic Stuff
#

set(CPACK_ALL_INSTALL_TYPES Minimal Standard Full)
set(CPACK_COMPONENT_GROUP_BIN_DISPLAY_NAME "End-User")
set(CPACK_COMPONENT_GROUP_DEV_DISPLAY_NAME "Developer")

set(CPACK_COMPONENT_EXE_DISPLAY_NAME "Executable")
set(CPACK_COMPONENT_EXE_GROUP bin)
set(CPACK_COMPONENT_EXE_INSTALL_TYPES Minimal Standard Full)
set(CPACK_COMPONENT_DLL_DISPLAY_NAME "Shared Runtime")
set(CPACK_COMPONENT_DLL_GROUP bin)
set(CPACK_COMPONENT_DLL_INSTALL_TYPES Minimal Standard Full)
set(CPACK_COMPONENT_DBG_DISPLAY_NAME "Debug Files (PDB)")
set(CPACK_COMPONENT_DBG_GROUP bin)
set(CPACK_COMPONENT_DBG_INSTALL_TYPES Full)

set(CPACK_COMPONENT_LIB_DEPENDS lic)
set(CPACK_COMPONENT_LIB_DISPLAY_NAME "Static Library")
set(CPACK_COMPONENT_LIB_GROUP dev)
set(CPACK_COMPONENT_LIB_INSTALL_TYPES Full)
set(CPACK_COMPONENT_INC_DEPENDS lic)
set(CPACK_COMPONENT_INC_DISPLAY_NAME "Header Files")
set(CPACK_COMPONENT_INC_GROUP dev)
set(CPACK_COMPONENT_INC_INSTALL_TYPES Standard Full)
set(CPACK_COMPONENT_LIC_DISPLAY_NAME "License Info")
set(CPACK_COMPONENT_LIC_GROUP dev)
set(CPACK_COMPONENT_LIC_INSTALL_TYPES Standard Full)
set(CPACK_COMPONENT_PRJ_DEPENDS lib dll)
set(CPACK_COMPONENT_PRJ_DISPLAY_NAME "CMake Export")
set(CPACK_COMPONENT_PRJ_GROUP dev)
set(CPACK_COMPONENT_PRJ_INSTALL_TYPES Full)

set(CPACK_COMPONENT_DOC_DISPLAY_NAME "Documentation")
set(CPACK_COMPONENT_DOC_INSTALL_TYPES Standard Full)

set(CPACK_PACKAGE_DESCRIPTION_FILE "${PROJECT_SOURCE_DIR}/README.md")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "WhatEver")
set(CPACK_PACKAGE_NAME "${PROJECT_NAME}")
set(CPACK_PACKAGE_VERSION "${PROJECT_VERSION}")
set(CPACK_PACKAGE_VERSION_MAJOR "${PROJECT_VERSION_MAJOR}")
set(CPACK_PACKAGE_VERSION_MINOR "${PROJECT_VERSION_MINOR}")
set(CPACK_PACKAGE_VERSION_PATCH "${PROJECT_VERSION_PATCH}")

include(CPack)

