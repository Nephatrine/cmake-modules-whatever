#
# Install Generated Binaries
#

include(GNUInstallDirs)

if(WHATEVER_TARGETS)
	foreach(WHATEVER_TARGET ${WHATEVER_TARGETS})
		if("${WHATEVER_TARGET}" MATCHES ".+_static$")
			set(WHATEVER_COMPONENT "lib")
		elseif("${WHATEVER_TARGET}" MATCHES ".+_shared$")
			set(WHATEVER_COMPONENT "dll")
		else()
			set(WHATEVER_COMPONENT "exe")
		endif()
		install(TARGETS "${WHATEVER_TARGET}"
			EXPORT ${PROJECT_NAME}
			COMPONENT ${WHATEVER_COMPONENT}
			RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
			LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
			ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR})
	endforeach()
endif()

if(MSVC)
	list(APPEND WHATEVER_CONFIGURATIONS "Debug")
	list(APPEND WHATEVER_CONFIGURATIONS "RelWithDebInfo")
	foreach(WHATEVER_CONFIGURATION ${WHATEVER_CONFIGURATIONS})
		install(DIRECTORY "${PROJECT_BINARY_DIR}/${WHATEVER_CONFIGURATION}/"
			DESTINATION ${CMAKE_INSTALL_BINDIR}
			CONFIGURATIONS ${WHATEVER_CONFIGURATION}
			COMPONENT dbg
			FILES_MATCHING PATTERN REGEX "[^_]+.pdb$")
		install(DIRECTORY "${PROJECT_BINARY_DIR}/${WHATEVER_CONFIGURATION}/"
			DESTINATION ${CMAKE_INSTALL_LIBDIR}
			CONFIGURATIONS ${WHATEVER_CONFIGURATION}
			COMPONENT lib
			FILES_MATCHING PATTERN "*_static.pdb")
	endforeach()
endif()

#
# Install Documentation
#

install(FILES "${PROJECT_SOURCE_DIR}/README.md"
	DESTINATION ${CMAKE_INSTALL_DOCDIR}
	COMPONENT doc)

install(FILES "${PROJECT_SOURCE_DIR}/LICENSE.md"
	DESTINATION ${CMAKE_INSTALL_DOCDIR}
	COMPONENT lic
	OPTIONAL)

if(WHATEVER_LICENSE)
	if(NOT "${WHATEVER_LICENSE}" STREQUAL "LICENSE.md")
		install(FILES "${PROJECT_SOURCE_DIR}/${WHATEVER_LICENSE}"
			DESTINATION ${CMAKE_INSTALL_DOCDIR}
			COMPONENT lic)
	endif()
endif()

if(WHATEVER_DOCUMENTATION)
	foreach(WHATEVER_DOCUMENT ${WHATEVER_DOCUMENTATION})
		install(FILES "${PROJECT_SOURCE_DIR}/${WHATEVER_DOCUMENT}"
			DESTINATION ${CMAKE_INSTALL_DOCDIR}
			COMPONENT doc)
	endforeach()
endif()

#
# Install Libraries
#

install(DIRECTORY "${PROJECT_SOURCE_DIR}/include/"
	DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}"
	COMPONENT inc
	OPTIONAL
	PATTERN "internal" EXCLUDE)

install(DIRECTORY "${PROJECT_BINARY_DIR}/include/"
	DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}"
	COMPONENT inc
	OPTIONAL
	PATTERN "internal" EXCLUDE)

#
# Install Export
#

install(EXPORT ${PROJECT_NAME}
	DESTINATION "${CMAKE_INSTALL_LIBDIR}/WhatEver"
	COMPONENT prj)

